#ifndef FMGV2DTRAIN_PBCC
#define FMGV2DTRAIN_PBCC

// User must provide: fmgv2D() and mgv2D().
//
// This transform won't compile on its own since the user must define
// the desired fmgv and mgv operators.  Include those operators
// and then include this file.  See Poisson2DfmgvTrain.pbcc for an example.

#include "ComputeError.pbcc"

transform fmgv2DTrain
from X[n,n], B[n,n]
through zeros[(n+1)/2,(n+1)/2]
to Y[n,n]
{
  zeros.cell(i,j) from() {
    return 0;
  }

  to (Y y) from (X x, B b, zeros z)
  {
    double error;
    double estimateError;
    double newError;
    double oldError;
    double change;
//    double prec[5] = {1e-1, 1e-3, 1e-5, 1e-7, 1e-9};
    double prec[5];
    prec[0] = 0.1;
    prec[1] = 0.001;
    prec[2] = 0.00001;
    prec[3] = 0.0000001;
    prec[4] = 0.000000001;

    int level = (int) log2(n-1);
    int iterations = 0;
    int ptr = 0;
//    int result[5] = {0, 0, 0, 0, 0};
    int result[5];
    result[0] = 0;
    result[1] = 0;
    result[2] = 0;
    result[3] = 0;
    result[4] = 0;

    printf("Accuracy levels: (%g, %g, %g, %g, %g)\n",
        prec[0], prec[1], prec[2], prec[3], prec[4]);

    printf("Training run for level %d\n", level);

    // compute actual by use of mgv2D 15 times
    MatrixRegion2D actual = MatrixRegion2D::allocate(n,n);
    mgv2D(actual, x, b, 15);

    // y is our working guess
    Copy2D(y, x);

    ComputeError(error, y, actual);
    printf("initial error = %g\n", error);

    // compute estimate
    fmgv2D(y, y, b, 0);
    ComputeError(estimateError, y, actual);
    printf("estimate error = %g\n", estimateError);

    newError = estimateError;
    oldError = error;
    change = 0;
    while (newError / error > prec[4] && (iterations < 5 || change < 1)) {
      iterations++;
      mgv2D(y, y, b, 1);
      ComputeError(newError, y, actual);
      change = newError / oldError;
      oldError = newError;
      printf("%d: error = %g, ratio = %g\n",
          iterations, newError, newError / error);
      while (ptr < 5 && newError / error <= prec[ptr]) {
        result[ptr++] = iterations;
      }
    }
    if (change >= 1) {
      printf("Failure to converge to highest specified precision\n");
    }
    printf("Level %d: {%d, %d, %d, %d, %d}\n", level, 
        result[0], result[1], result[2], result[3], result[4], result[5]);
  }
}

#endif // FMGV2DTRAIN_PBCC

