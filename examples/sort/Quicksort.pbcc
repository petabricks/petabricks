#ifndef QUICKSORT_PBCC
#define QUICKSORT_PBCC

#include "../utilities.pbcc"
#include "Sort.pbcc"

//#define SORTSUBARRAY QuicksortSubArray
#define SORTSUBARRAY SortSubArray

%{

void swap1D(MatrixRegion1D &A, int x, int y)
{
  double temp = A.cell(x);
  A.cell(x) = A.cell(y);
  A.cell(y) = temp;
}

int partition(MatrixRegion1D &A)
{
  int left = 0;
  int right=A.count()-1;
  double pivotValue = A.cell(right);
  int storeIndex = left;
  for (int i = left; i < right; i++) {
    if (A.cell(i) <= pivotValue) {
      swap1D(A, i, storeIndex);
      storeIndex++;
    }
  }
  swap1D(A, storeIndex, right);
  return storeIndex;
}

%}

transform QuicksortSubArray
from IN[n]
to OUT[n], TEMP[n]
{
  to (OUT out, TEMP temp) from (IN in)
  {
    int p = partition(out);
    if(p>1)
      SORTSUBARRAY(out .region(0,p),
                   temp.region(0,p), 
                   in  .region(0,p));
    if(p<out.count()-2)
      SORTSUBARRAY(out .region(p+1,n),
                   temp.region(p+1,n), 
                   in  .region(p+1,n));
  }
}

transform QuickSort
from IN[n]
to OUT[n]
{
  to (OUT out) from (IN in)
  {
    Copy1D(out, in);
    MatrixRegion1D temp = MatrixRegion1D::allocate(n);
    QuicksortSubArray(out, temp, in);
  }
}

#endif // QUICKSORT_PBCC

