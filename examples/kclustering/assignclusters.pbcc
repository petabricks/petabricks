#ifndef ASSIGNCLUSTERS_PBCC
#define ASSIGNCLUSTERS_PBCC

//#ifndef
//#define k 3
//#endif

#include "distance.pbcc"

transform AssignClustersGen
to X[n,2], C[n/10,2], A[n]
{
  X.cell(i,j) from() { return PetabricksRuntime::randInt(1,100); }
  C.cell(i,j) from() { return PetabricksRuntime::randInt(1,100); }
  A.cell(i) from() { return PetabricksRuntime::randInt(0,n/10-1); }
}


transform AssignClusters
from X[n,2], C[k,2], A[n]
through D[n,k], ctemp[n]
to Anew[n], cflag
generator AssignClustersGen
{

	to (D.cell(i,j) d) from (X.column(i) x, C.column(j) c)
	{
		Distance(d,x,c);
	}

	Anew.cell(i) from(D.column(i) d)
	{
		IndexT   minindex = 0;
		ElementT mindist=d.cell(0);
		for (int j=1; j<k; j++) {
			if (d.cell(j) < mindist) {
				minindex=j;
				mindist = d.cell(j);
			}
		}
    return minindex;
	}
	
  ctemp.cell(i) from(A.cell(i) aold, Anew.cell(i) anew) {
    return aold!=anew;
  }

	cflag from (ctemp c) {
		int i;
		ElementT sum=0;
		for (i=0; i<n; i++) {
			sum+=c.cell(i);
		}
		return sum;
	}

}


#endif // ASSIGNCLUSTERS_PBCC
