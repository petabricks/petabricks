#ifndef MULTIPLY_PBCC
#define MULTIPLY_PBCC

#include "../simple/add.pbcc"
#include "../simple/transpose.pbcc"

%{
#include "cblas.h"

#ifdef PRINT
inline void printArray(int height, int width, ElementT *array){
  int i, j;

  for(i=0; i<height; ++i) {
    for(j=0; j<width; ++j) {
      printf("%2.1lf ", array[i*width+ j]);
    }
    printf("\n");
  }
}
#endif

inline ElementT *toArray(petabricks::ConstMatrixRegion2D &matrix) {
  ElementT *array;
  
  IndexT rows = matrix.height();
  IndexT columns = matrix.width();

#ifdef PRINT
  printf("Matrix size: %d rows, %d columns\n", rows, columns);
#endif

  array = new ElementT[rows * columns];
  
  for(IndexT j=0; j<columns; ++j) {
    for(IndexT i=0; i<rows; ++i) {
      array[j*rows + i] = matrix.cell(j,i);
    }
  }
  
  return array;
}

inline void toMatrix(ElementT* array, petabricks::MatrixRegion2D &matrix) {
  IndexT rows = matrix.height();
  IndexT columns = matrix.width();

  for(IndexT j=0; j<columns; ++j) {
    for(IndexT i=0; i<rows; ++i) {
      matrix.cell(j,i) = array[j*rows + i];
    }
  }
  
  return;
}

void gemm_wrap(int rowA, int colB1, int rowB, float alpha, float* arrayA, int colA, float* arrayB, int colB2, float beta, float* arrayAB, int colAB) {
  cblas_sgemm(CblasColMajor, CblasNoTrans, CblasNoTrans, rowA, colB1, rowB, alpha, arrayA, colA, arrayB, colB2, beta, arrayAB, colAB);
}

void gemm_wrap(int rowA, int colB1, int rowB, double alpha, double* arrayA, int colA, double* arrayB, int colB2, double beta, double* arrayAB, int colAB) {
  cblas_dgemm(CblasColMajor, CblasNoTrans, CblasNoTrans, rowA, colB1, rowB, alpha, arrayA, colA, arrayB, colB2, beta, arrayAB, colAB);
}
%}

transform MatrixMultiply
from A[c,h], B[w,c] 
to AB[w,h]
{
  //compute a cell the straightforward way
  AB.cell(x,y) from(A.row(y) a, B.column(x) b){
    ElementT sum=0;
    for(IndexT i=0, w=a.width(); i<w; ++i)
      sum+=a.cell(i)*b.cell(i);
    return sum;
  }

  //recursively chop the matrix in half
  recursive(c) 
  to(AB ab)
  from( A.region(0,   0, c/2, h  ) a1,
        A.region(c/2, 0, c,   h  ) a2,
        B.region(0,   0, w,   c/2) b1,
        B.region(0, c/2, w,   c  ) b2)
  {
    MatrixRegion2D tmp = MatrixRegion2D::allocate(w,h);
    spawn MatrixMultiply(ab,  a1, b1);
    spawn MatrixMultiply(tmp, a2, b2);
    sync;
    MatrixAdd(ab, ab, tmp);
  }
  
  recursive(w)
  to(AB.region(0,   0, w/2, h  ) ab1,
     AB.region(w/2, 0, w,   h  ) ab2)
  from( A a,
        B.region(0,   0, w/2, h  ) b1,
        B.region(w/2, 0, w,   h  ) b2)
  {
    spawn MatrixMultiply(ab1, a, b1);
    spawn MatrixMultiply(ab2, a, b2);
  }

  recursive(h)
  to(AB.region(0, 0,   w, h/2) ab1,
     AB.region(0, h/2, w, h  ) ab2)
  from(A.region(0,   0, w,   h/2) a1,
       A.region(0, h/2, w,   h  ) a2,
       B b)
  {
    spawn MatrixMultiply(ab1, a1, b);
    spawn MatrixMultiply(ab2, a2, b);
  }
  
  
  //Usual O(n^3) matrix multiply
  to(AB ab) from (A a, B b) {
    for(IndexT i=0, maxI=a.height(); i<maxI; ++i) {
      for(IndexT j=0, maxJ=b.width(); j<maxJ; ++j) {
        ab.cell(j,i) = 0;
        for(IndexT k=0, maxK=a.width(); k<maxK; ++k) {
	  ab.cell(j,i) += a.cell(k,i) * b.cell(j,k);
	}
      }
    }
  }
  
  to(AB ab) from (A a, B b) {
    int rowA=a.height();
    int colA=a.width();

    int rowB=b.height();
    int colB=b.width();

    int rowAB=ab.height();
    int colAB=ab.width();

    ElementT alpha=1;
    ElementT beta=0;
    
    if(colA != rowB) {
      fprintf(stderr, "Unable to perform a matrix multiplication. Wrong input size.\n"
	      "#columns of matrix A = %d\n"
	      "#rows of matrix B = %d\n"
	      "They should be equal!\n", colA, rowB);
      exit(4);
    }
    
    ElementT* arrayA = toArray(a);
    ElementT* arrayB = toArray(b);
    
    #ifdef PRINT
    printArray(rowA, colA, arrayA);
    printArray(rowB, colB, arrayB);
    #endif
    
    ElementT* arrayAB = new ElementT[rowAB*colAB];
    
    gemm_wrap(rowA, colB, rowB, alpha, arrayA, colA, arrayB, colB, beta, arrayAB, colAB);
    
    #ifdef PRINT
    printArray(rowAB, colAB, arrayAB);
    #endif
    
    toMatrix(arrayAB, ab);
    
    delete arrayA;
    delete arrayB;
    delete arrayAB;
  }
}

transform MatrixMultiplyTransposed
from A[n,n], B[n,n] 
through T[n,n]
to C[n,n]
{
  // no transpose
  to(C c, T t) from(A a, B b) {
    MatrixMultiply(c, a, b);
  }
  // transpose b
  to(C c, T t) from(A a, B b) {
    Transpose(t,b);
    MatrixMultiply(c, a, t.transposed());
  }
  // transpose a
  to(C c, T t) from(A a, B b) {
    Transpose(t,a);
    MatrixMultiply(c, t.transposed(), b);
  }
}

#endif // MULTIPLY_PBCC
